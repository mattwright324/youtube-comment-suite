// Setup Gradle
plugins {
    id "application"
    id "org.beryx.runtime" version "1.12.2"
}

repositories {
    mavenCentral()
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(16)
        vendor = JvmVendorSpec.BELLSOFT
    }
}

// Setup application, dependencies
group = "io.mattw.youtube"

application {
    mainClassName = "io.mattw.youtube.commentsuite.CommentSuite"
}

dependencies {
    implementation group: 'com.google.apis', name: 'google-api-services-youtube', version: 'v3-rev222-1.25.0'
    implementation group: 'org.apache.commons', name: 'commons-lang3', version: '3.12.0'
    implementation group: 'org.apache.commons', name: 'commons-text', version: '1.9'
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.8.6'
    implementation group: 'com.google.guava', name: 'guava', version: '30.1.1-jre'
    implementation group: 'org.jsoup', name: 'jsoup', version: '1.13.1'
    implementation group: 'org.xerial', name: 'sqlite-jdbc', version: '3.34.0'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.14.1'
    implementation group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.14.1'

    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.7.1'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.8.0'
    testImplementation group: 'org.mockito', name: 'mockito-junit-jupiter', version: '3.8.0'
}

// Setup release
// https://simply-how.com/custom-java-runtime
runtime {
    addOptions("--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages")

    // For Liberica JDK
    addModules("javafx.base", "javafx.controls", "javafx.fxml")
}

tasks.runtime.doLast {
    copy {
        from("src/main/resources")
        into("$buildDir/image/bin")
    }
}

