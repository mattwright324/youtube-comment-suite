// Setup Gradle
plugins {
    id "application"
    id "org.beryx.runtime" version "1.12.2"
}

repositories {
    mavenCentral()
}

// Setup application, dependencies
group = "io.mattw.youtube.commentsuite"

application {
    mainClassName = "io.mattw.youtube.commentsuite.CommentSuite"
}

dependencies {
    implementation 'com.google.apis:google-api-services-youtube:v3-rev222-1.25.0'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.apache.commons:commons-text:1.9'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.guava:guava:30.1.1-jre'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'org.xerial:sqlite-jdbc:3.34.0'
    implementation 'org.apache.logging.log4j:log4j-api:2.14.1'
    implementation 'org.apache.logging.log4j:log4j-core:2.14.1'

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.7.1'
    testImplementation 'org.mockito:mockito-core:3.8.0'
    testImplementation 'org.mockito:mockito-junit-jupiter:3.8.0'
}

// Setup release
runtime {
    addOptions("--strip-debug", "--compress", "2", "--no-header-files", "--no-man-pages")

    jpackage {
        imageOptions = ["--icon", "src/main/resources/io/mattw/youtube/commentsuite/img/icon.ico"]
    }

    launcher {
        // Fix handshake_failure on http calls
        // https://github.com/beryx/badass-jlink-plugin/issues/21#issuecomment-472914695
        jvmArgs = ['-Djdk.tls.client.protocols=TLSv1.2']
    }

    // Until there is support for JDK 16 with beryx runtime plugin,
    // switch to Liberica 15 to run suggestModules, then switch back.
    // Experiment with additive = true when fixed
    addModules(
            "java.compiler",
            "java.datatransfer",
            "java.desktop",
            "java.logging",
            "java.management",
            "java.naming",
            "java.rmi",
            "java.scripting",
            "java.sql",
            "java.xml",
            "javafx.base",
            "javafx.controls",
            "javafx.fxml",
            "javafx.graphics",
            "javafx.web",
            "jdk.unsupported"
    )
}
